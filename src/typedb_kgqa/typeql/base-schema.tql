define
# SYSTEM-ATTRIBUTES
## Source of info
attribute text-content, value string;
attribute meta-page-title, value string; # Uniquely identifies the owner.

# For now, attribute ownership sources just go on the owner.
entity meta-document, owns meta-page-title, owns text-content;

relation meta-knowledge-source, relates knowledge @card(1), relates source;
meta-document plays meta-knowledge-source:source;

# Attributes
# Every property of an entity has to be a property.
attribute base-property @abstract;

entity base-entity @abstract, owns meta-page-title @card(0..1), plays meta-knowledge-source:knowledge;
relation base-relation  @abstract, owns meta-page-title @card(0..1), plays meta-knowledge-source:knowledge;

attribute name @abstract, sub base-property, value string;
attribute normalised-name sub name;
base-entity owns normalised-name @card(1);
attribute alternate-name sub name;
base-entity owns alternate-name @card(0..);

############
# ENTITIES #
############
# AGENT
entity agentic-entity sub base-entity;

entity person, sub agentic-entity;
entity organisation, sub agentic-entity;

entity base-inanimate-object @abstract, sub base-entity; # Buildings, books, etc.

#########
# PLACE
#########
entity base-place sub base-entity;
entity country sub base-place;
entity state sub base-place;
entity city sub base-place;


# ---- Geo-containment ----
relation base-located-in sub base-relation,
    relates base-contained,
    relates base-containing;

relation located-in sub base-located-in,
    relates located-subject as base-contained,
    relates containing-location as base-containing;

base-inanimate-object plays located-in:located-subject;
organisation plays located-in:located-subject;
base-place plays located-in:containing-location;

relation place-located-in sub base-located-in,
   relates contained-location as base-contained,
   relates containing-location as base-containing;

base-place,
   plays place-located-in:contained-location, 
   plays place-located-in:containing-location;

#########
# EVENT #
#########
attribute occured-at, value datetime;
attribute lasted-till, value datetime;

# Makes it easy for comparisons
occured-at sub base-property; 
lasted-till sub base-property;

relation base-event @abstract, sub base-relation,
   relates base-involved,
   owns occured-at,
   owns lasted-till;

# event-location
relation event-location sub base-located-in,
   relates located-event as base-contained @card(1),
   relates containing-location as base-containing @card(1..);
base-event plays event-location:located-event;
base-place plays event-location:containing-location;


# Correlation between events
relation event-correlation, sub base-relation,
   relates event @card(2..);
base-event plays event-correlation:event;

# Some subtypes
# Both of people and inanimate objects - e.g. birth of a person, publication of a book
relation base-life-cycle-event @abstract, sub base-event, 
   relates base-in-life-of as base-involved @card(1);

###############
# ASSOCIATION #
###############
# Typical relations like marriage, parent. `associated` must be specialised
relation base-association, sub base-relation,
   relates base-associated @card(0..);

base-entity plays base-association:base-associated;
