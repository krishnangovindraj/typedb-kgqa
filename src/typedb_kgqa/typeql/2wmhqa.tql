define

####################################
# 2WikiMultiHopQA Schema Extension #
####################################
# Extends base-schema.tql to model entities and relationships
# needed for 2WikiMultiHopQA multi-hop questions.
#
# Key question patterns covered:
#   - Family relations (parent, spouse, grandparent, in-law)
#   - Person biography (birth/death place & date, nationality, cause of death)
#   - Creative works (film direction, song performance, composition)
#   - Organisations (location, founding, publishing)
#   - Comparisons (born/died first, same country, older/younger)

###############
# ATTRIBUTES  #
###############

attribute gender sub base-property, value string;
attribute member-role, value string;
attribute cause-of-death sub base-property, value string;

######################
# ENTITY EXTENSIONS  #
######################

# -- Person --
person owns gender;

# -- Organisation --
entity school sub organisation;
entity company sub organisation;
entity band sub organisation;

# -- Inanimate objects --
entity base-creative-work sub base-inanimate-object;

entity film sub base-creative-work;
entity song sub base-creative-work;
entity album sub base-creative-work;

# -- Award --
entity award sub base-entity;

##############
# RELATIONS  #
##############

# ---- Family ----

relation parentage sub base-association,
    relates parent as base-associated,
    relates child as base-associated;

person plays parentage:parent,
    plays parentage:child;

relation marriage sub base-association,
    relates spouse as base-associated @card(2);

person plays marriage:spouse;

# person life events
relation base-person-life-event sub base-life-cycle-event,
    relates person as base-in-life-of;
person plays base-person-life-event:person;

relation wedding sub base-person-life-event;
relation birth sub base-person-life-event;
relation death sub base-person-life-event,
    owns cause-of-death;
relation burial sub base-person-life-event;

# organisation life events
relation establishment sub base-life-cycle-event,
    relates established as base-in-life-of;
organisation plays establishment:established;

# creative-work life events
relation release sub base-life-cycle-event,
    relates released-work as base-in-life-of;
base-creative-work plays release:released-work;

# ---- Nationality ----

relation nationality sub base-association,
    relates national as base-associated,
    relates nation as base-associated;

person plays nationality:national;
country plays nationality:nation;

# ---- Employment ----

relation employment sub base-association,
    relates employee as base-associated,
    relates employer as base-associated;

person plays employment:employee;
organisation plays employment:employer;

# ---- Education ----

relation education sub base-association,
    relates student as base-associated,
    relates institution as base-associated;

person plays education:student;
organisation plays education:institution;

# ---- Film direction ----

relation film-direction sub base-association,
    relates director as base-associated,
    relates directed-film as base-associated;

person plays film-direction:director;
film plays film-direction:directed-film;

# ---- Song performance ----

relation song-performance sub base-association,
    relates performer as base-associated,
    relates performed-song as base-associated;

person plays song-performance:performer;
song plays song-performance:performed-song;

# ---- Composition (song or film score) ----

relation composition sub base-association,
    relates composer as base-associated,
    relates composed-work as base-associated;

person plays composition:composer;
base-creative-work plays composition:composed-work;

# ---- Band membership ----
# member-role captures instrument/position (e.g. "keyboardist", "lead guitarist")

relation band-membership sub base-association,
    relates member as base-associated,
    relates member-band as base-associated,
    owns member-role;

person plays band-membership:member;
band plays band-membership:member-band;

# ---- Awards ----

relation award-receipt sub base-association,
    relates recipient as base-associated,
    relates received-award as base-associated;

person plays award-receipt:recipient;
award plays award-receipt:received-award;

# ---- Publishing ----

relation publishing sub base-association,
    relates publisher as base-associated,
    relates publication as base-associated;

organisation plays publishing:publisher;
base-creative-work plays publishing:publication;


# Hacks with "other"
attribute other-type-specifier value string;
entity other-creative-work sub base-creative-work,
    owns other-type-specifier;

relation other-association sub base-association,
    relates other-associated as base-associated @card(0..),
    owns other-type-specifier;
base-entity plays other-association:other-associated;

relation other-event sub base-event,
    relates other-involved as base-involved @card(0..),
    owns other-type-specifier;
base-entity plays other-event:other-involved;


# Helper functions:
fun ident($x: base-entity) -> base-entity:
match $x isa base-entity;
return first  $x;

fun ternary($cond: boolean, $if: base-entity, $else: base-entity) -> base-entity:
match 
  { $cond == true; let $x = ident($if); } or { $cond == false; let $x = ident($else); };
return first $x;

fun less_or_equal($first: base-property, $second: base-property) -> boolean:
match 
  $first <= $second;
return check;

fun is_same($first: base-entity, $second: base-entity) -> boolean:
match $first is $second;
return check;

fun located_in_transitive($contained: base-entity) -> { base-place }:
match 
    $_ isa base-located-in, links (base-contained: $contained, base-containing: $containing);
    $ret isa base-place;
    { $ret is $containing; } or { let $ret in located_in_transitive($containing); };
return { $ret };
